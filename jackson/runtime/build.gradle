plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group 'com.gitlab.faerytea.mapper.jackson'
version '1.0'

sourceCompatibility = 1.6

repositories {
    jcenter()
    maven { url "https://dl.bintray.com/faerytea/jars" }
}

dependencies {
    api 'com.gitlab.faerytea.mapper:annotations:0.9'
    api 'com.fasterxml.jackson.core:jackson-core:2.10.+'
}


task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

bintray {
    def properties = new Properties() {
        {
            load(new File('../../core/local.properties').newInputStream())
        }
    }
    user = properties.'bintray.user'
    key = properties.'bintray.key'
    pkg {
        repo = 'jars'
        name = project.name
        licenses = [ 'Apache-2.0' ]
        vcsUrl = 'https://gitlab.com/faerytea/mapper.git'
        version {
            name = project.version
            released = new Date(1583790000000L)
        }
    }
    publications = ['mapperJacksonRuntime']
}

publishing {
    publications {
        mapperJacksonRuntime(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}
