buildscript {
    repositories {
        jcenter()
    }
}

plugins {
    id 'java'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group 'com.gitlab.faerytea.mapper.jackson'
version '1.0'

sourceCompatibility = 1.8

repositories {
    mavenLocal()
    jcenter()
    maven { url "https://dl.bintray.com/faerytea/jars" }
}

dependencies {
    implementation 'com.gitlab.faerytea.mapper:generators-base:0.7.+'
    implementation 'org.jetbrains:annotations:16.0.2'
    implementation 'com.gitlab.faerytea.mapper:annotations:0.9'
    implementation 'com.squareup:javapoet:1.11.1'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        jacksonMapperProcessor(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}

bintray {
    def properties = new Properties() {
        {
            load(new File('../../core/local.properties').newInputStream())
        }
    }
    user = properties.'bintray.user'
    key = properties.'bintray.key'
    pkg {
        repo = 'jars'
        name = project.name
        licenses = ['LGPL-3.0']
        vcsUrl = 'https://gitlab.com/faerytea/mapper.git'
        version {
            name = project.version
            released = new Date(1583790000000L)
        }
    }
    publications = ['jacksonMapperProcessor']
}