plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group 'com.gitlab.faerytea.mapper'
version '0.6.0'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    implementation 'org.jetbrains:annotations:16.0.2'
    api 'com.gitlab.faerytea.mapper:annotations:0.8'
    api 'com.squareup:javapoet:1.11.1'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

bintray {
    user = rootProject.bintrayUsername
    key = rootProject.bintrayKey
    pkg {
        repo = 'jars'
        name = project.name
        licenses = ['LGPL-3.0']
        vcsUrl = 'https://gitlab.com/faerytea/mapper.git'
        version {
            name = project.version
            desc = 'Alpha release'
            released = new Date(1580623678585L)
            vcsTag = 'alpha'
        }
    }
    publications = ['mapperGeneratorsBase']
}

publishing {
    publications {
        mapperGeneratorsBase(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}
